{- Impredicative Encoding of Inductive Types:
   - Unit.
   Copyright (c) Groupoid Infinity, 2014-2018. -}

module impredicative where
import proto
import path
-- import iso_pi
--  iso_sigma

{-
   Church Encoding Nat:

      Nat = (X: U) -> (X -> X) -> X -> X

   Impredicative Encoding Unit:

      Unit = (X: U) -> isSet X -> X -> X
      Unit_Encoding = (one: Unit)
                    * ((X Y: U) (x: isSet X) (y:isSet Y) (f:X->Y)
                           -> naturality X Y f (one X x) (one Y y))

   Impredicative Encoding Nat:

      Nat  = (X: U) -> isSet X -> (X -> X) -> (X -> X)
      Nat_Encoding = (one: Nat)
                    * ((X Y: U) (x: isSet X) (y:isSet Y) (f:X->Y)
                           -> naturality X Y f (one X x) (one Y y))

   Motivation is to have algebra structure that could be used with any coherent carrier.
   Impredicativity means that we can land inductive type in any universe.
   E.g. one can change the predicate from isSet to isProp for truncations or to higher n-types.
   You can also model HIT using impredicative encoding. Universes could be impredicative
   and univalent at the same time.

      Truncation ||A|| parametrized by (A:U) type = (X: U) -> isProp X -> (A -> X) -> X
      S^1 = (X:U) -> isGroupoid X -> (x:X) -> Path X x x -> X
      Arbitrary (A:U) type = (X: U) -> isSet X -> (A -> X) -> X
-}

-- (1) Signature
unitEnc': U = (X: U) -> isSet X -> X -> X

-- (2) Property
naturality (X Y:U)(f:X->Y)(a:X->X)(b:Y->Y): U = Path (X->Y)(o X X Y f a)(o X Y Y b f)
isUnitEnc (one: unitEnc'): U = (X Y:U)(x:isSet X)(y:isSet Y)(f:X->Y)->naturality X Y f (one X x)(one Y y)

-- (3) Former/Intro/Elim/Beta/Eta Full-pack
unitEnc: U = (x: unitEnc') * isUnitEnc x
unitEncStar: unitEnc = (\(X:U)(_:isSet X)->idfun X,\(X Y: U)(_:isSet X)(_:isSet Y)->refl(X->Y))
unitEncRec  (C: U) (s: isSet C) (c: C): unitEnc -> C = \(z: unitEnc) -> z.1 C s c
unitEncBeta (C: U) (s: isSet C) (c: C): Path C (unitEncRec C s c unitEncStar) c = refl C c
unitEncEta (z: unitEnc): Path unitEnc unitEncStar z = undefined
unitEncInd (P: unitEnc -> U) (a: unitEnc): P unitEncStar -> P a
  = subst unitEnc P unitEncStar a (unitEncEta a)
unitEncCondition (n: unitEnc'): isProp (isUnitEnc n)
  =  \ (f g: isUnitEnc n) -> <h> \ (x y: U) -> \ (X: isSet x) -> \ (Y: isSet y)
  -> \ (F: x -> y) -> <i> \ (R: x) -> Y (F (n x X R)) (n y Y (F R))
       (<j> (f x y X Y F @ j) R) (<j> (g x y X Y F @ j) R) @ h @ i

{-
   [1]. https://github.com/sspeight93/Papers/
   [2]. https://homotopytypetheory.org/2017/10/11/impredicative-encodings-of-inductive-types-in-hott/
   [3]. https://www.newton.ac.uk/files/seminar/20170711090010001-1009680.pdf
-}
